mapscripts NewBarkTown_ElmsLab_MapScripts {

}

const PROF_ELM = 1
const CYNDAQUIL_BALL = 2
const TOTODILE_BALL = 3
const CHIKORITA_BALL = 4
const ELM_AIDE = 5

script ElmsLabIntroLeft {
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkRight)
    call(ElmsLab_Intro)
}
script ElmsLab_Intro {
    if(var(VAR_INTRO_STATE) == 1) {
        lock
        applymovement(OBJ_EVENT_ID_PLAYER, PlayerWalkToElm)
        waitmovement(0)
        msgbox(
            format( "PROFESSOR ELM: There you are, {PLAYER}!\p"
                    "I need to ask you a favor to do with my research.\p"
                    "As you know, POKéMON are carried in POKéBALLs nowadays, but before they were invented we used simpler devices and even walked around with POKéMON.\p"
                    "I'm sure you've noticed your friend LYRA running around with her MARILL.\p"
                    "While POKéBALLs are still needed to catch POKéMON, I wonder what benefits can be had from traveling with POKéMON in this way?\p"
                    "THAT is what I want to find out, and as part of that, I want to give you a POKéMON of your own.\p"
                    "I want you to walk beside this POKéMON and see if this grows bonds or changes how they grow and evolve.\p"
            )
        )
        applymovement(PROF_ELM, Common_Movement_FaceRight)
        waitmovement(0)
        msgbox(format("I have three POKéMON you can choose from over here.\pI just caught them and you'll be their first partner, {PLAYER}!\pGo on, pick one!"))
        release
    }
}

script ElmsLab_CyndaquilBall {
    if(var(VAR_INTRO_STATE) > 1) {
        call(ElmsLab_BallAfterChoosing)
    } else {
        lock
        showmonpic(SPECIES_CYNDAQUIL, 10, 3)
        msgbox(format("ELM: You'll take CYNDAQUIL, the fire POKéMON?"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == 1){
            hidemonpic
            msgbox(ElmsLab_ChoseStarterText)
            setflag(FLAG_CHOSE_CYNDAQUIL)
            setflag(FLAG_SYS_POKEMON_GET)
            playfanfare(MUS_OBTAIN_ITEM)
            waitfanfare
            givemon(SPECIES_CYNDAQUIL, 5, ITEM_ORAN_BERRY)
            bufferspeciesname(STR_VAR_1, SPECIES_CYNDAQUIL)
            msgbox(gText_NicknameThisPokemon, MSGBOX_YESNO)
            if (var(VAR_RESULT) == 1) {
                call(Common_EventScript_GetGiftMonPartySlot)
                call(Common_EventScript_NameReceivedPartyMon)
            }
            setvar(VAR_STARTER_MON, 0)
            removeobject(CYNDAQUIL_BALL)
            applymovement(OBJ_EVENT_ID_PLAYER, AfterCyndaquilMovement)
            waitmovement(0)
            release
            call(ElmsLab_AfterStarterChoice)
        } else {
            call(ElmsLab_DontWantThatOne)
        }
    }
}

script ElmsLab_ChikoritaBall {
    if(var(VAR_INTRO_STATE) > 1) {
        call(ElmsLab_BallAfterChoosing)
    } else {
        lock
        showmonpic(SPECIES_CHIKORITA, 10, 3)
        msgbox(format("ELM: You'll take CHIKORITA, the grass POKéMON?"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == 1){
            hidemonpic
            msgbox(ElmsLab_ChoseStarterText)
            setflag(FLAG_CHOSE_CHIKORITA)
            setflag(FLAG_SYS_POKEMON_GET)
            playfanfare(MUS_OBTAIN_ITEM)
            waitfanfare
            givemon(SPECIES_CHIKORITA, 5, ITEM_ORAN_BERRY)
            bufferspeciesname(STR_VAR_1, SPECIES_CHIKORITA)
            msgbox(gText_NicknameThisPokemon, MSGBOX_YESNO)
            if (var(VAR_RESULT) == 1) {
                call(Common_EventScript_GetGiftMonPartySlot)
                call(Common_EventScript_NameReceivedPartyMon)
            }
            setvar(VAR_STARTER_MON, 1)
            removeobject(CHIKORITA_BALL)
            applymovement(OBJ_EVENT_ID_PLAYER, AfterChikoritaMovement)
            waitmovement(0)
            release
            call(ElmsLab_AfterStarterChoice)
        } else {
            call(ElmsLab_DontWantThatOne)
        }
    }
}
script ElmsLab_TotodileBall {
    if(var(VAR_INTRO_STATE) > 1) {
        call(ElmsLab_BallAfterChoosing)
    } else {
        lock
        showmonpic(SPECIES_TOTODILE, 10, 3)
        msgbox(format("ELM: You'll take TOTODILE, the grass POKéMON?"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == 1){
            hidemonpic
            msgbox(ElmsLab_ChoseStarterText)
            setflag(FLAG_CHOSE_TOTODILE)
            setflag(FLAG_SYS_POKEMON_GET)
            playfanfare(MUS_OBTAIN_ITEM)
            waitfanfare
            givemon(SPECIES_TOTODILE, 5, ITEM_ORAN_BERRY)
            bufferspeciesname(STR_VAR_1, SPECIES_TOTODILE)
            msgbox(gText_NicknameThisPokemon, MSGBOX_YESNO)
            if (var(VAR_RESULT) == 1) {
                call(Common_EventScript_GetGiftMonPartySlot)
                call(Common_EventScript_NameReceivedPartyMon)
            }
            setvar(VAR_STARTER_MON, 1)
            removeobject(TOTODILE_BALL)
            applymovement(OBJ_EVENT_ID_PLAYER, AfterTotodileMovement)
            waitmovement(0)
            release
            call(ElmsLab_AfterStarterChoice)
        } else {
            call(ElmsLab_DontWantThatOne)
        }
    }
}

script ElmsLab_BallAfterChoosing {
    lock
    msgbox(format("It contains a POKéMON caught by PROFESSOR ELM."))
    release
}

movement AfterCyndaquilMovement {
    walk_left
    face_up
}

movement AfterTotodileMovement {
    walk_left * 2
    face_up
}

movement AfterChikoritaMovement {
    walk_left * 3
    face_up
}


text ElmsLab_ChoseStarterText {
    format("ELM: I think that's a great POKéMON too!")
}

script ElmsLab_DontWantThatOne {
    hidemonpic
    msgbox(format("ELM: Think it over carefully. Your partner is a very important choice."))
    release
}

script ElmsLab_AfterStarterChoice {
    lock
    setvar(VAR_INTRO_STATE, 2)
    applymovement(PROF_ELM, Common_Movement_FaceDown)
    waitmovement(0)
    msgbox(
        format(
            "PROFESSOR ELM: I have something that I think would be a great way for you to start your POKéMON journey!\p"
            "I have a colleague called MR. POKéMON who keeps finding weird things and raving to me about his discoveries.\p"
            "I just got an e-mail from him saying this time it's real.\p"
            "An intriguing message, but we're very busy with our research right now. He doesn't live too far away, so if you go in our place, you'll be doing us a great favor and get to explore with your new POKéMON friend!"
        )
    )
    msgbox(format("What do you say, will you help out?"), MSGBOX_YESNO)
    while (var(VAR_RESULT) != 1) {
        msgbox("But we really need your help {PLAYER}!\pWon't you please reconsider?", MSGBOX_YESNO)
    }
    msgbox(format("ELM: Great! MR. POKéMON lives a little bit beyond CHERRYGROVE, the next city over.\pIt's almost a direct route there, so you can't miss it.\pJust in case, here's my phone number so you can call me if anything comes up."))
    setvar(VAR_TEMP_1, 1)
    release
}

movement PlayerWalkToElm {
    walk_up * 6
}

script ElmsLab_Aide {
    lock
    msgbox(format("There are only two of us, so we're always busy."))
    release
}

script ElmsLab_CantLeave {
    lock
    applymovement(PROF_ELM, Common_Movement_FaceDown)
    waitmovement(0)
    msgbox(format("ELM: Wait! Where are you going?"))
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkUp)
    waitmovement(0)
    release
}

script ElmsLab_CantLeaveN {
    lock
    applymovement(PROF_ELM, Common_Movement_FaceUp)
    waitmovement(0)
    msgbox(format("ELM: Wait! Where are you going?"))
    applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkLeft)
    waitmovement(0)
    applymovement(PROF_ELM, Common_Movement_FaceRight)
    waitmovement(0)
    release
}

script ElmsLab_ProfElm {
    lockall
    faceplayer
    switch (var(VAR_INTRO_STATE)) {
        case 1:
            msgbox(format("ELM: Go on, pick one!"))
        case 2:
            msgbox(format("ELM: If a wild POKéMON appears in the tall grass, be sure to let yours battle it!"))
        default:
            msgbox(format("COMING SOON"))

    }
    releaseall
}

script ElmsLab_TrashcanText {
    lockall
    msgbox(format("The wrapper from the RAGECANDYBAR that PROF. ELMS ate is in there.."))
    releaseall
}

script ElmsLab_PCText {
    lockall
    msgbox(format("OBSERVATIONS ON POKéMON EVOLUTION AND GROWTH - A COMPREHENSIVE REVIEW BY PROFESSOR ELM\p...\p...\pThis seems like a very complicated paper."))
    releaseall
}

script ElmsLab_HealingMachine {
    lockall
    if (var(VAR_INTRO_STATE) == 1){
        msgbox(format("A machine for healing POKéMON."))
    }
    else {
        msgbox(format("Would you like to heal your POKéMON?"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == 1){
            playfanfare(MUS_HEAL)
	        waitfanfare
            special(HealPlayerParty)
        }
    }
    releaseall
}

script ElmsLab_AideGivesPotionL {
    lockall
    applymovement(ELM_AIDE, AideWalkLeft)
    waitmovement(0)
    call(ElmsLab_AideGivesPotion)
    applymovement(ELM_AIDE, AideReturnsLeft)
    waitmovement(0)
    setvar(VAR_TEMP_1, 0)
    releaseall
}

script ElmsLab_AideGivesPotionR {
    lockall
    applymovement(ELM_AIDE, AideWalkRight)
    waitmovement(0)
    call(ElmsLab_AideGivesPotion)
    applymovement(ELM_AIDE, AideReturnsRight)
    waitmovement(0)
    setvar(VAR_TEMP_1, 0)
    releaseall
}

script ElmsLab_AideGivesPotion {
    msgbox(format("{PLAYER}, I want you to have this for your errand."))
    giveitem(ITEM_POTION)
    playfanfare(MUS_OBTAIN_ITEM)
    waitfanfare
}

movement AideWalkLeft {
    walk_right * 2
    face_up
}

movement AideReturnsLeft {
    walk_left * 2
    face_down
}

movement AideWalkRight {
    walk_right * 3
    face_up
}

movement AideReturnsRight {
    walk_left * 3
    face_down
}
script ElmsLab_OpenWindow {
    lockall
    msgbox(format("A pleasant breeze blows in through the open window..."))
    releaseall
}